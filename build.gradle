buildscript {
    ext {
        springBootVersion = '1.5.6.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('io.github.swagger2markup:swagger2markup-gradle-plugin:1.3.1')
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.github.swagger2markup'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    docOutputDir = file("${rootDir}/docs/")
    swaggerOutputFile = file("${docOutputDir}/swagger.json")
    asciiDocOutputDir = file("${docOutputDir}/asciidoc")
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compileOnly('org.projectlombok:lombok')
    compile('io.swagger:swagger-annotations:1.5.16')

    testCompileOnly('org.projectlombok:lombok')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    testCompile('io.springfox:springfox-swagger2:2.7.0')
    testCompile('io.springfox:springfox-bean-validators:2.7.0')
}

test {
    systemProperty 'swaggerOutputFile', swaggerOutputFile
}

convertSwagger2markup {
    dependsOn test

    swaggerInput = swaggerOutputFile
    outputDir file("${asciiDocOutputDir}/generated")

    config = [
            'swagger2markup.pathsGroupedBy'                          : 'TAGS',
    ]
}
